//--------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation.  All rights reserved.
// THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY
// OF ANY KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING BUT NOT
// LIMITED TO THE IMPLIED WARRANTIES OF MERCHANTABILITY AND
// FITNESS FOR A PARTICULAR PURPOSE.
//--------------------------------------------------------------------------

OBJECT Page 50000 Performance Profiler
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=Performance Profiler;
  }
  PROPERTIES
  {
    Permissions=;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table50000;
    PageType=Worksheet;
    OnInit=BEGIN
             TargetSessionID := SESSIONID;
           END;

    OnOpenPage=BEGIN
                 // Assume that profiler hasn't been started.
                 // This assumption might be wrong.
                 ProfilerStarted := FALSE;
                 Rec.SETFILTER("Session ID", '=%1', TargetSessionID);
                 PerformanceProfiler := PerformanceProfiler.EtwPerformanceProfiler();
               END;

    OnAfterGetRecord=VAR
                       LinesHit@1003 : Integer;
                     BEGIN
                     END;

    ActionList=ACTIONS
    {
      { 15      ;    ;ActionContainer;
                      Name=Code Coverage Actions;
                      ActionContainerType=ActionItems }
      { 14      ;1   ;Action    ;
                      Name=Start;
                      Promoted=Yes;
                      Enabled=NOT ProfilerStarted;
                      PromotedIsBig=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ProfilerRecord@1000 : Record 89260;
                                 i@1001 : Integer;
                               BEGIN
                                 CLEAR(Rec);
                                 Rec.SETFILTER("Session ID", '=%1', TargetSessionID);
                                 Rec.DELETEALL;

                                 PerformanceProfiler.Start(TargetSessionID, 1);

                                 ProfilerStarted := TRUE;
                               END;
                                }
      { 3       ;1   ;Action    ;
                      Name=Stop;
                      Promoted=Yes;
                      Enabled=ProfilerStarted;
                      PromotedIsBig=Yes;
                      Image=Stop;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 WaitForDataToBeCollected;
                                 PerformanceProfiler.Stop;

                                 ProfilerStarted := FALSE;
                                 I := 1;

                                 WHILE (PerformanceProfiler.CallTreeMoveNext) DO BEGIN
                                   CLEAR(Rec);
                                   Rec.INIT;
                                   Rec.Id := I;
                                   Rec."Session ID" := TargetSessionID;
                                   Rec.Indentation := PerformanceProfiler.CallTreeCurrentStatementIndentation;
                                   Rec."Object Type" := PerformanceProfiler.CallTreeCurrentStatementOwningObjectType;
                                   Rec."Object ID" := PerformanceProfiler.CallTreeCurrentStatementOwningObjectId;
                                   Rec."Line No" := PerformanceProfiler.CallTreeCurrentStatementLineNo;
                                   Rec.Statement := PerformanceProfiler.CallTreeCurrentStatement;
                                   Rec.Duration := PerformanceProfiler.CallTreeCurrentStatementDurationMs;
                                   Rec.HitCount := PerformanceProfiler.CallTreeCurrentStatementHitCount;
                                   Rec.INSERT;

                                   I += 1;
                                 END;
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=Clear Codeunit 1 calls;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ClearFilter;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 codeUnit1Call@1000 : Boolean;
                               BEGIN
                                 codeUnit1Call := FALSE;

                                 FINDFIRST;

                                 REPEAT

                                   IF (Indentation = 0) THEN BEGIN
                                     IF (("Object Type" = "Object Type"::Codeunit) AND ("Object ID" = 1)) THEN
                                       codeUnit1Call := TRUE
                                     ELSE
                                       codeUnit1Call := FALSE;
                                   END;

                                   IF (codeUnit1Call) THEN
                                    DELETE;

                                 UNTIL NEXT = 0;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 16  ;1   ;Group     ;
                CaptionML=ENU=Configuration;
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=Target Session ID;
                Lookup=Yes;
                SourceExpr=TargetSessionID;
                TableRelation="Active Session"."Session ID";
                OnValidate=BEGIN
                             Rec.SETFILTER("Session ID", '=%1', TargetSessionID);
                           END;
                            }

    { 2   ;1   ;Group     ;
                Name=Call Tree;
                Editable=FALSE;
                IndentationColumnName=Indentation;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 7   ;2   ;Field     ;
                SourceExpr="Object Type";
                Enabled=FALSE;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Object ID" }

    { 11  ;2   ;Field     ;
                SourceExpr="Line No" }

    { 5   ;2   ;Field     ;
                SourceExpr=Statement }

    { 9   ;2   ;Field     ;
                SourceExpr=Duration;
                Style=Attention }

    { 13  ;2   ;Field     ;
                SourceExpr=HitCount }

    { 6   ;2   ;Field     ;
                SourceExpr=Id }

    { 8   ;2   ;Field     ;
                SourceExpr="Session ID" }

  }
  CODE
  {
    VAR
      ProgressDialog@1004 : Dialog;
      PerformanceProfiler@1000 : DotNet "'EtwPerformanceProfiler, Version=1.0.0.0, Culture=neutral, PublicKeyToken=null'.EtwPerformanceProfiler.EtwPerformanceProfiler";
      ProfilerStarted@1001 : Boolean INDATASET;
      I@1002 : Integer;
      TargetSessionID@1003 : Integer INDATASET;
      PleaseWaitCollectingData@1005 : TextConst 'ENU=Collecting performance data \\Please wait #1';

    LOCAL PROCEDURE WaitForDataToBeCollected@4();
    VAR
      SecondsToWait@1000 : Integer;
    BEGIN
      SecondsToWait := 3;
      ProgressDialog.OPEN(PleaseWaitCollectingData);
      WHILE SecondsToWait > 0 DO BEGIN
        ProgressDialog.UPDATE(1,STRSUBSTNO('%1 s',SecondsToWait));
        SLEEP(1000);
        SecondsToWait -= 1;
      END;
      ProgressDialog.CLOSE;
    END;

    BEGIN
    END.
  }
}

